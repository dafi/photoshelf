apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:${rootProject.ext.androidXAppCompatLibVersion}"
    implementation "androidx.recyclerview:recyclerview:${rootProject.ext.androidXRecyclerviewLibVersion}"
    implementation "androidx.cardview:cardview:${rootProject.ext.androidXCardViewLibVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${rootProject.ext.androidXConstraintlayoutLibVersion}"
    implementation "androidx.preference:preference-ktx:${rootProject.ext.androidXPreferenceLibVersion}"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:${rootProject.ext.androidXSwiperefreshlayoutLibVersion}"
    implementation "androidx.core:core-ktx:${rootProject.ext.androidXCoreKtx}"
    implementation "androidx.lifecycle:lifecycle-extensions:${rootProject.ext.androidXLifecycleExtensions}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${rootProject.ext.androidxLifecycleViewmodelKtx}"

    implementation "com.google.android.material:material:${rootProject.ext.androidXMaterialLibVersion}"
    implementation "com.google.android:flexbox:${rootProject.ext.flexboxLibVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${rootProject.ext.kotlinxCoroutinesCore}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${rootProject.ext.kotlinxCoroutinesAndroid}"

    implementation "com.dropbox.core:dropbox-core-sdk:${rootProject.ext.dropboxCoreLibVersion}"

    implementation "org.greenrobot:eventbus:${rootProject.ext.eventbusLibVersion}"

    implementation "com.squareup.picasso:picasso:${rootProject.ext.picassoLibVersion}"
    implementation "com.squareup.retrofit2:retrofit:${rootProject.ext.retrofitLibVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${rootProject.ext.retrofitConverterGsonLibVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${rootProject.ext.okhttp3LoggingInterceptorLibVersion}"
    implementation "com.squareup.okio:okio:${rootProject.ext.okio}"

    implementation "com.google.code.gson:gson:${rootProject.ext.gsonLibVersion}"

    implementation "com.ternaryop:utils:${rootProject.ext.ternaryOpUtilLibVersion}"
    implementation "com.ternaryop.photoshelf:domselector-coroutines:${rootProject.ext.ternaryOpDomSelectorCoroutinesLibVersion}"
    implementation "com.ternaryop:feedly-coroutines:${rootProject.ext.ternaryOpFeedlyCoroutinesLibVersion}"
    implementation "com.ternaryop:tumblr-coroutines:${rootProject.ext.ternaryOpTumblrCoroutinesLibVersion}"

    testImplementation "junit:junit:${rootProject.ext.junitLibVersion}"
    androidTestImplementation "androidx.annotation:annotation:${rootProject.ext.androidXAnnotationLibVersion}"
    androidTestImplementation "androidx.test:rules:${rootProject.ext.androidXRulesLibVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${rootProject.ext.androidXEspressoCoreLibVersion}"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            def keystoreFile = new File("$rootDir/app/build-keystore.properties")
            if (keystoreFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(keystoreFile))
                storeFile file(props.get("key.store"))
                storePassword props.get("key.store.password")
                keyAlias props.get("key.alias")
                keyPassword props.get("key.alias.password")
            }
        }
    }

    defaultConfig {
        applicationId "com.ternaryop.photoshelf"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled true
        versionCode versionCodeByTask()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // default value for android emulator
        buildConfigField "String", "PHOTOSHELF_API_PREFIX", "\"http://10.0.2.2:8080/image/\""
    }

    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            buildConfigField "String" , "PHOTOSHELF_API_PREFIX" , "\"http://visualdiffer.com/image/\""
        }
        debug {
            applicationIdSuffix ".debug"
        }
        debugGenymotion {
            initWith debug
            // used with genymotion
            buildConfigField "String", "PHOTOSHELF_API_PREFIX", "\"http://10.0.3.2:8080/image/\""
        }
        debugRealDeviceLocal {
            // use from real devices to access to local network
            initWith debug
            buildConfigField "String", "PHOTOSHELF_API_PREFIX", "\"http://192.168.0.2:8080/image/\""
        }
        debugRealDeviceProd {
            // use from real devices to access to production env
            initWith debug
            buildConfigField "String" , "PHOTOSHELF_API_PREFIX" , "\"http://visualdiffer.com/image/\""
        }
    }

    sourceSets {
        debugGenymotion {
            root = "src/debug"
        }
        debugRealDeviceLocal {
            root = "src/debug"
        }
        debugRealDeviceProd {
            root = "src/debug"
        }
    }
    // Workaround for "Method ... not mocked." in test cases
    testOptions {
        unitTests.returnDefaultValues = true
    }

    // Needed by dropbox
    // see http://stackoverflow.com/questions/31912459/duplicate-lib-file-copied-in-apk-meta-inf-license-txt-error-in-andorid-studio#31912566
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

tasks.withType(JavaCompile) {
    compileTask ->
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
repositories {
    mavenCentral()
}

def versionCodeByTask() {
    def props = new Properties()
    def versionFile = new File("$rootDir/app/version.properties")
    def versionCode = 1
    if (versionFile.exists()) {
        props.load(new FileReader(versionFile))
        versionCode = props["versionCode"]?.isInteger() ? props["versionCode"].toInteger() : 1
    }

    // Increment the versionCode only for specific tasks (eg the assemble release tasks)
    def runTasks = gradle.startParameter.taskNames
    def incrementableTaskNames = ["aR", "assembleRelease"]
    if (incrementableTaskNames.any { name -> name in runTasks }) {
        ++versionCode
        props["versionCode"] = versionCode.toString()
        props.store(new FileWriter(versionFile), null)
    }
    return versionCode
}

apply plugin: 'kotlin-android-extensions'
