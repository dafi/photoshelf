// Top-level build file where you can add configuration options common to all sub-projects/modules.

// https://github.com/ben-manes/gradle-versions-plugin

// run ./gradlew dependencyUpdates
apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: 'kotlin-kapt'
apply plugin: "io.gitlab.arturbosch.detekt"
apply from: 'https://raw.githubusercontent.com/dafi/gradle-scripts/main/dependencyUpdatesReplacer.gradle'

buildscript {
    ext.kotlin_version = '1.8.0'
    ext {
        minSdkVersion = 26
        targetSdkVersion = 33
        compileSdkVersion = 33

        def runTasks = gradle.startParameter.taskNames
        def releaseTaskNames = ["aR", "assembleRelease", "bundleRelease"]
        if (releaseTaskNames.any { name -> name in runTasks }) {
            println "Skipping preview SDK (if any) for production build"
        } else {
            def localProperties = new Properties()
            localProperties.load(new FileInputStream(rootProject.file("local.properties")))
            def targetSdkVersionDevPreview = localProperties['target.sdk.version.dev.preview']
            def compileSdkVersionDevPreview = localProperties['compile.sdk.version.dev.preview']
            if (targetSdkVersionDevPreview == null || compileSdkVersionDevPreview == null) {
                println "Preview SDK not defined, using SDK version $targetSdkVersion"
            }
            if (targetSdkVersionDevPreview?.trim() && compileSdkVersionDevPreview?.trim()) {
                println "Using preview SDK: $targetSdkVersionDevPreview"
                targetSdkVersion = targetSdkVersionDevPreview
                compileSdkVersion = compileSdkVersionDevPreview
            }
        }

        androidXAppCompatLibVersion = '1.7.0-alpha01'
        androidXRecyclerviewLibVersion = '1.3.0-rc01'
        androidXAnnotationLibVersion = '1.6.0-alpha01'
        androidXMaterialLibVersion = '1.9.0-alpha01'
        androidXConstraintlayoutLibVersion = '2.2.0-alpha05'
        androidXPreferenceLibVersion = '1.2.0'
        androidXSwiperefreshlayoutLibVersion = '1.2.0-alpha01'
        androidXCoreKtx = '1.10.0-alpha01'
        androidXLifecycleVersion = '2.6.0-alpha04'
        androidXWorkLibVersion = '2.8.0-rc01'
        androidXFragmentVersion = '1.6.0-alpha04'

        flexboxLibVersion = '3.0.0'

        kotlinxCoroutinesCore = '1.6.4'
        kotlinxCoroutinesAndroid = '1.6.4'

        hiltAndroidVersion = '2.44.2'

        moshiLibVersion = '1.14.0'

        ternaryOpUtilLibVersion = '1.4.12'
        ternaryOpDrawerLibVersion = '1.0.2'
        ternaryOpWidgetLibVersion = '1.0.5'
        ternaryOpCrashReporterLibVersion = '1.0.2'
        ternaryOpApiCoroutinesLibVersion = '1.1.4'
        ternaryOpDomSelectorCoroutinesLibVersion = '1.0.9'
        ternaryOpFeedlyCoroutinesLibVersion = '1.0.8'
        ternaryOpTumblrCoroutinesLibVersion = '1.0.9'

        dropboxCoreLibVersion = '5.4.4'

        coilLibVersion = '2.2.2'

        okhttp3LoggingInterceptorLibVersion = '5.0.0-alpha.11'

        detektLibVersion = "1.22.0"

        junitLibVersion = '4.13.2'
        androidXRulesLibVersion = '1.5.0'
        androidXEspressoCoreLibVersion = '3.5.1'
        androidxTestExtJunit = '1.1.5'
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "com.github.ben-manes:gradle-versions-plugin:0.44.0"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektLibVersion"

        classpath "gradle.plugin.project:plugin:1.4.0"

        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltAndroidVersion"
    }
}

allprojects {
    repositories {
        mavenCentral()
        // mavenRepo urls: "file:///home/user/.m2/repository"
        mavenLocal()
        google()
    }
}

apply plugin: "com.jraska.module.graph.assertion"

allprojects {
    // JUnit 4.13 causes "Unresolved reference" so it's necessary to change resolution strategy
    configurations.all {
        resolutionStrategy.force "junit:junit:$junitLibVersion"
    }
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        buildUponDefaultConfig = true
        config = files("${project.rootDir}/config/detekt/detekt.yml")
        parallel = true
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektLibVersion"
    }

    if (name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }
    android {
        lint {
            lintConfig file("${project.rootDir}/config/lint/lint.xml")
        }
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                // -Xjvm-default=all required by coil https://coil-kt.github.io/coil/getting_started/#java-8
                freeCompilerArgs = [
                        '-Xjvm-default=all',
                        "-Xjavac-arguments='-Xlint:unchecked -Xlint:deprecation'"
                ]
                jvmTarget = '1.8'
            }
        }
    }
}
