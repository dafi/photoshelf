// Top-level build file where you can add configuration options common to all sub-projects/modules.

// https://github.com/ben-manes/gradle-versions-plugin

// run ./gradlew dependencyUpdates
apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: 'kotlin-kapt'
apply plugin: "io.gitlab.arturbosch.detekt"
apply from: 'https://raw.githubusercontent.com/dafi/gradle-scripts/main/dependencyUpdatesReplacer.gradle'

buildscript {
    ext.kotlin_version = '1.5.31'
    ext {
        minSdkVersion = 26
        targetSdkVersion = 31
        compileSdkVersion = 31

        targetSdkVersionDevPreview = targetSdkVersion //'S' //targetSdkVersion // 'Q'
        compileSdkVersionDevPreview = compileSdkVersion //'android-S' //compileSdkVersion // 'android-Q'

        // workaround to change sdk for release APK
        // see https://issuetracker.google.com/issues/36988361
        def runTasks = gradle.startParameter.taskNames
        def incrementableTaskNames = ["aR", "assembleRelease"]
        if (!incrementableTaskNames.any { name -> name in runTasks }) {
            targetSdkVersion = targetSdkVersionDevPreview
            compileSdkVersion = compileSdkVersionDevPreview
        }

        androidXAppCompatLibVersion = '1.4.0-rc01'
        androidXRecyclerviewLibVersion = '1.3.0-alpha01'
        androidXAnnotationLibVersion = '1.3.0'
        androidXMaterialLibVersion = '1.5.0-alpha05'
        androidXConstraintlayoutLibVersion = '2.1.1'
        androidXPreferenceLibVersion = '1.2.0-alpha02'
        androidXSwiperefreshlayoutLibVersion = '1.2.0-alpha01'
        androidXCoreKtx = '1.7.0'
        androidXLifecycleExtensions = '2.2.0'
        androidXLifecycleViewmodelKtx = '2.4.0'
        androidXLifecycleCommonJava8 = '2.4.0'
        androidXWorkLibVersion = '2.7.0'

        flexboxLibVersion = '3.0.0'

        kotlinxCoroutinesCore = '1.5.2'
        kotlinxCoroutinesAndroid = '1.5.2'

        hiltAndroidVersion = '2.40'
        hiltAndroidCompilerVersion = '2.40'

        moshiKotlinCodegenLibVersion = '1.12.0'

        ternaryOpUtilLibVersion = '1.4.9'
        ternaryOpDrawerLibVersion = '1.0.1'
        ternaryOpWidgetLibVersion = '1.0.4'
        ternaryOpCrashReporterLibVersion = '1.0.0'
        ternaryOpApiCoroutinesLibVersion = '1.1.3'
        ternaryOpDomSelectorCoroutinesLibVersion = '1.0.7'
        ternaryOpFeedlyCoroutinesLibVersion = '1.0.5'
        ternaryOpTumblrCoroutinesLibVersion = '1.0.7'

        dropboxCoreLibVersion = '5.0.0'

        coilLibVersion = '1.4.0'

        okhttp3LoggingInterceptorLibVersion = '5.0.0-alpha.2'

        junitLibVersion = '4.13'
        androidXRulesLibVersion = '1.4.1-alpha03'
        androidXEspressoCoreLibVersion = '3.5.0-alpha03'
        androidxTestExtJunit = '1.1.4-alpha03'
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "com.github.ben-manes:gradle-versions-plugin:0.39.0"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.18.1"

        classpath "gradle.plugin.project:plugin:1.4.0"

        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.40'
    }
}

allprojects {
    repositories {
        mavenCentral()
        // mavenRepo urls: "file:///home/user/.m2/repository"
        mavenLocal()
        google()
    }
}

apply plugin: "com.jraska.module.graph.assertion"

allprojects {
    // JUnit 4.13 causes "Unresolved reference" so it's necessary to change resolution strategy
    configurations.all {
        resolutionStrategy.force "junit:junit:$junitLibVersion"
    }
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    detekt {
        config = files("${project.rootDir}/detekt-photoshelf.yml")
        parallel = true
    }
    if (name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }
    android {
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                // -Xjvm-default=all required by coil https://coil-kt.github.io/coil/getting_started/#java-8
                freeCompilerArgs = ['-Xjvm-default=all']
                jvmTarget = '1.8'
            }
        }
    }
}
