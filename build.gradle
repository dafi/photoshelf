// Top-level build file where you can add configuration options common to all sub-projects/modules.

// https://github.com/ben-manes/gradle-versions-plugin

// run ./gradlew dependencyUpdates
apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jetbrains.kotlin.jvm"

buildscript {
    ext.kotlin_version = '1.4.0'
    ext {
        minSdkVersion = 26
        targetSdkVersion = 30
        compileSdkVersion = 30

        targetSdkVersionDevPreview = targetSdkVersion // 'Q'
        compileSdkVersionDevPreview = compileSdkVersion // 'android-Q'

        // workaround to change sdk for release APK
        // see https://issuetracker.google.com/issues/36988361
        def runTasks = gradle.startParameter.taskNames
        def incrementableTaskNames = ["aR", "assembleRelease"]
        if (!incrementableTaskNames.any { name -> name in runTasks }) {
            targetSdkVersion = targetSdkVersionDevPreview
            compileSdkVersion = compileSdkVersionDevPreview
        }

        buildToolsVersion = '28.0.3'
        androidXCardViewLibVersion = '1.0.0'
        androidXAppCompatLibVersion = '1.3.0-alpha01'
        androidXRecyclerviewLibVersion = '1.2.0-alpha05'
        androidXAnnotationLibVersion = '1.2.0-alpha01'
        androidXMaterialLibVersion = '1.3.0-alpha02'
        androidXConstraintlayoutLibVersion = '2.0.0-rc1'
        androidXPreferenceLibVersion = '1.1.1'
        androidXSwiperefreshlayoutLibVersion = '1.2.0-alpha01'
        androidXCoreKtx = '1.5.0-alpha01'
        androidXLifecycleExtensions = '2.2.0'
        androidXLifecycleViewmodelKtx = '2.3.0-alpha06'
        androidXLifecycleCommonJava8 = '2.3.0-alpha06'
        androidXWorkLibVersion = '2.4.0'

        flexboxLibVersion = '2.0.1'

        kotlinxCoroutinesCore = '1.3.9'
        kotlinxCoroutinesAndroid = '1.3.9'

        hiltAndroidVersion = '2.28.3-alpha'
        hiltAndroidCompilerVersion = '2.28.3-alpha'
        hiltLifecycleViewmodelVersion = '1.0.0-alpha02'
        hiltCompilerVersion = '1.0.0-alpha02'

        ternaryOpUtilLibVersion = '1.4.9'
        ternaryOpDrawerLibVersion = '1.0.1'
        ternaryOpWidgetLibVersion = '1.0.2'
        ternaryOpCrashReporterLibVersion = '1.0.0'
        ternaryOpApiCoroutinesLibVersion = '1.1.0'
        ternaryOpDomSelectorCoroutinesLibVersion = '1.0.5'
        ternaryOpFeedlyCoroutinesLibVersion = '1.0.2'
        ternaryOpTumblrCoroutinesLibVersion = '1.0.5'

        dropboxCoreLibVersion = '3.1.5'

        coilLibVersion = '0.11.0'

        retrofitLibVersion = '2.7.1'
        gsonLibVersion = '2.8.6'
        retrofitConverterGsonLibVersion = '2.7.1'
        okhttp3LoggingInterceptorLibVersion = '4.8.1'
        okio = '2.4.3'

        junitLibVersion = '4.13'
        androidXRulesLibVersion = '1.3.0-rc03'
        androidXEspressoCoreLibVersion = '3.3.0-rc03'
        androidxTestExtJunit = '1.1.2-rc03'
    }
    repositories {
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "com.github.ben-manes:gradle-versions-plugin:0.29.0"
        classpath "gradle.plugin.project:plugin:1.3.2"

        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.28.3-alpha'
    }
}

allprojects {
    repositories {
        jcenter()
        // mavenRepo urls: "file:///home/user/.m2/repository"
        mavenLocal()
        google()
    }
}

apply plugin: "com.jraska.module.graph.assertion"

allprojects {
    // JUnit 4.13 causes "Unresolved reference" so it's necessary to change resolution strategy
    configurations.all {
        resolutionStrategy.force "junit:junit:$junitLibVersion"
    }
}

subprojects {
    if (name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }
    android {
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                // -Xjvm-default=all required by coil https://coil-kt.github.io/coil/getting_started/#java-8
                freeCompilerArgs = ['-Xjvm-default=all']
                jvmTarget = '1.8'
            }
        }
    }
}
