import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// Top-level build file where you can add configuration options common to all sub-projects/modules.

// https://github.com/ben-manes/gradle-versions-plugin

// run ./gradlew dependencyUpdates
apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: 'kotlin-kapt'
apply plugin: "io.gitlab.arturbosch.detekt"
apply from: 'https://raw.githubusercontent.com/dafi/gradle-scripts/main/dependencyUpdatesReplacer.gradle'
apply plugin: "com.jraska.module.graph.assertion"

buildscript {
    ext.kotlin_version = libs.kotlin
    ext {
        minSdkVersion = 26
        targetSdkVersion = 35
        compileSdkVersion = 33

        def runTasks = gradle.startParameter.taskNames
        def releaseTaskNames = ["aR", "assembleRelease", "bundleRelease"]
        if (releaseTaskNames.any { name -> name in runTasks }) {
            println "Skipping preview SDK (if any) for production build"
        } else {
            def localProperties = new Properties()
            localProperties.load(new FileInputStream(rootProject.file("local.properties")))
            def targetSdkVersionDevPreview = localProperties['target.sdk.version.dev.preview']
            def compileSdkVersionDevPreview = localProperties['compile.sdk.version.dev.preview']
            if (targetSdkVersionDevPreview == null || compileSdkVersionDevPreview == null) {
                println "Preview SDK not defined, using SDK version $targetSdkVersion"
            }
            if (targetSdkVersionDevPreview?.trim() && compileSdkVersionDevPreview?.trim()) {
                println "Using preview SDK: $targetSdkVersionDevPreview"
                targetSdkVersion = targetSdkVersionDevPreview
                compileSdkVersion = compileSdkVersionDevPreview
            }
        }
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath(libs.gradle)
        classpath(libs.kotlin.plugin)

        classpath(libs.benmanes.version.deps)
        classpath(libs.detekt.plugin)

        classpath(libs.gradle.plugin)

        classpath(libs.hilt.android.gradle.plugin)
    }
}

allprojects {
    repositories {
        mavenCentral()
        // mavenRepo urls: "file:///home/user/.m2/repository"
        mavenLocal()
        google()
    }
}

allprojects {
    // JUnit 4.13 causes "Unresolved reference" so it's necessary to change resolution strategy
    configurations.configureEach {
        resolutionStrategy.force(libs.junit)
    }
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        buildUponDefaultConfig = true
        config = files("${project.rootDir}/config/detekt/detekt.yml")
        parallel = true
    }

    dependencies {
        detektPlugins(libs.detekt.plugin.formatting)
    }

    if (name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }
    android {
        lint {
            lintConfig file("${project.rootDir}/config/lint/lint.xml")
        }
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        tasks.withType(KotlinCompile).configureEach {
            compilerOptions {
                // -Xjvm-default=all required by coil https://coil-kt.github.io/coil/getting_started/#java-8
                freeCompilerArgs = [
                        '-Xjvm-default=all',
                        "-Xjavac-arguments='-Xlint:unchecked -Xlint:deprecation'"
                ]
                jvmTarget = JvmTarget.JVM_1_8
            }
        }
    }
}
